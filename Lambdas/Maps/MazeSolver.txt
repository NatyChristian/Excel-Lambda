Nom : MazeSolver
Formule : 
LAMBDA(mp,st,[Targets],[No_Diag],[Cus_Dir], LET(   M, 1000000,   rcst, MAP(st, LAMBDA(a, ROW(INDIRECT(a)) * M + COLUMN(INDIRECT(a)))),   dir, {1000000,-1000000,1,-1,1000001,-1000001,999999,-999999},   xRC, IF(ISOMITTED(Cus_Dir), TAKE(dir,, 8 - 4 * IF(ISOMITTED(No_Diag), 0, No_Diag)), TOROW(Cus_Dir,,3)),   rn, ROW(mp),   cn, COLUMN(mp),   rcn, rn * M + cn,   ir, @rn - 1,   ic, @cn - 1,   na, HSTACK(0, 0),    fx, LAMBDA(lfx,pool,ft,     LET(       expl, TOCOL(TAKE(ft,,1) + xRC),       er, INT(expl / M) - ir,       ec, MOD(expl, M) - ic,       fd, TOCOL(DROP(ft,,1) + SEQUENCE(, COUNTA(xRC), 0, 0)),       pd, IFNA(XLOOKUP(expl, TAKE(pool,,1), DROP(pool,,1),, ,2), "X"),       ift, SORT(         IF(           er * ec,           LET(             nd, INDEX(mp, er, ec),             IF(NOT(ISNUMBER(nd)), na,               IF(nd + fd < pd, HSTACK(expl, fd + nd), na)             )           ),           na         ),         {1,2}, {1,-1}       ),       nft, LET(         au, UNIQUE(TAKE(ift,,1)),         a, HSTACK(au, VLOOKUP(au, ift, 2)),         FILTER(a, TAKE(a,,1) > 0, HSTACK(0, 0))       ),       npool, LET(         b, SORT(VSTACK(pool, nft), {1,2}, {1,-1}),         bu, UNIQUE(TAKE(b,,1)),         HSTACK(bu, VLOOKUP(bu, b, 2))       ),       IF(SUM(nft) = 0, pool, lfx(lfx, npool, nft))     )   ),    rs, fx(fx, IFNA(HSTACK(rcst, 0), 0), IFNA(HSTACK(rcst, 0), 0)),    result, IF(     ISOMITTED(Targets),     IFNA(VLOOKUP(rcn, rs, 2, FALSE), "X"),     LET(       tgt, MAP(Targets, LAMBDA(a, ROW(INDIRECT(a)) * M + COLUMN(INDIRECT(a)))),       IFNA(VLOOKUP(tgt, rs, 2, FALSE), "X")     )   ),   result ) )
